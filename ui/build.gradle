plugins {
  id "com.moowork.node" version "0.12"
}

def frontendDir = "${project.projectDir}/src/frontend"

node {
  version = '8.8.1'
  npmVersion = '5.4.2'
  download = true
  workDir = file("${frontendDir}/node")
  nodeModulesDir = file("${frontendDir}")
}

sourceSets {
  main {
    resources {
      srcDirs = ["${frontendDir}/dist", "$projectDir/src/main/resources"]
    }
  }
}

task cleanDistFolder(type: Delete) {
  //noinspection GroovyAssignabilityCheck
  delete 'src/frontend/dist'
}

tasks.clean.dependsOn(cleanDistFolder)

task buildUi(type: NpmTask) {
  workingDir = "${frontendDir}"
  args = ['run', 'build']
}

npm_install {
  workingDir = "${frontendDir}"
}

buildUi.dependsOn(npm_install)

processResources.dependsOn(buildUi)

jar.dependsOn(buildUi)

dependencies {
  compile 'org.springframework.cloud:spring-cloud-starter-zuul'
  compile 'org.springframework.cloud:spring-cloud-starter-oauth2'
  compile "org.springframework.security:spring-security-jwt"
  compile 'org.apache.httpcomponents:httpclient'
}
